import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.1.5.RELEASE' apply false
}

subprojects { subproject ->

    subproject.apply plugin: 'io.spring.dependency-management'
    subproject.apply plugin: 'java'

    group = 'io.pivotal.conductor.worker'
    version = '0.1.0-SNAPSHOT'
    sourceCompatibility = '11.0'

    repositories { jcenter() }

    dependencies {
        testImplementation('org.junit.jupiter:junit-jupiter-api')
        testImplementation('org.assertj:assertj-core')
        testImplementation('org.mockito:mockito-core')
        testImplementation('org.mockito:mockito-junit-jupiter')
        testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
    }

    dependencyManagement {
        imports { mavenBom(SpringBootPlugin.BOM_COORDINATES) }

        dependencies {
            dependency("com.netflix.conductor:conductor-client:${conductorClientVersion}")
            dependency("org.springframework.security.oauth:spring-security-oauth2:${springSecurityOAuth2Version}")
            dependency("org.cloudfoundry:cloudfoundry-client-reactor:${cloudfoundryClientVersion}")
            dependency("org.cloudfoundry:cloudfoundry-operations:${cloudfoundryClientVersion}")
            dependency("org.eclipse.jgit:org.eclipse.jgit:${jgitVersion}")
            dependency("com.github.tomakehurst:wiremock-jre8:${wiremockServerVersion}")
            dependency("org.hamcrest:hamcrest:${hamcrestVersion}")
        }
    }

    test { useJUnitPlatform() }
}
